\chapter{Model of Execution}


\begin{figure}[h]
\begin{lstlisting}[language=Coq]
Inductive execute : tpm_state * state -> command -> tpm_state * state -> Prop :=

| E_Hash : forall stTPM st m,
    In m st ->
    execute (stTPM, st)
            (TPM2_Hash m)
            (hash m :: stTPM, hash m :: st)

| E_CheckHash : forall stTPM st m,
    In (hash m) st ->
    In m st ->
    execute (stTPM, st)
            (CheckHash (hash m) m)
            (stTPM, st)

| E_SignNR : forall stTPM st m i d f,
    In (privateKey (Private i NonRestricting Signing d f)) stTPM ->
    In m st ->
    execute (stTPM, st)
            (TPM2_Sign m (Private i NonRestricting Signing d f))
            (stTPM, signature m (Private i NonRestricting Signing d f) :: st)

| E_SignR : forall stTPM st m i d f,
    In (privateKey (Private i Restricting Signing d f)) stTPM ->
    In m stTPM ->
    execute (stTPM,st)
            (TPM2_Sign m (Private i Restricting Signing d f))
            (stTPM, signature m (Private i Restricting Signing d f) :: st)
\end{lstlisting}
\end{figure}   
      
\begin{figure}[h]
\begin{lstlisting}[language=Coq]           
| E_Certify : forall stTPM st i r s d f i0 r0 d0 f0,
    In (privateKey (Private i r s d f)) stTPM -> 
    In (privateKey (Private i0 r0 Signing d0 f0)) stTPM ->
    execute (stTPM, st)
            (TPM2_Certify (Public i r s d f) (Private i0 r0 Signing d0 f0))
            (stTPM, signature (TPM2B_Attest (Public i r s d f)) (Private i0 r0 Signing d0 f0) :: st)

| E_CheckSig : forall stTPM st m i r s d f,
    In (signature m (Private i r s d f)) st ->
    In (publicKey (Public i r s d f)) st ->
    execute (stTPM, st)
            (CheckSig (signature m (Private i r s d f)) (Public i r s d f))
            (stTPM, st)

| E_MakeCredential : forall stTPM st n g i,
    In n st ->
    In (randomNum g) st ->
    In (publicKey (Public i Restricting NonSigning Decrypting Fixing)) st ->
    execute (stTPM, st)
            (TPM2_MakeCredential n g (Public i Restricting NonSigning Decrypting Fixing))
            (stTPM, encryptedCredential n g (Public i Restricting NonSigning Decrypting Fixing) :: st)

| E_ActivateCredential : forall stTPM st n g i r s d f i0 r0 s0 d0 f0,
    In (encryptedCredential n g (Public i r s d f)) st ->
    In (privateKey (Private i r s d f)) stTPM ->
    In (privateKey (Private i0 r0 s0 d0 f0)) stTPM ->
    execute (stTPM, n::st) (CheckHash n (publicKey (Public i0 r0 s0 d0 f0))) (stTPM, n::st) ->
    execute (stTPM, st)
            (TPM2_ActivateCredential (encryptedCredential n g (Public i r s d f)) 
                                     (Private i r s d f)
                                     (Private i0 r0 s0 d0 f0)) 
            (stTPM, randomNum g :: st)
\end{lstlisting}
\end{figure}   
      
\begin{figure}[h]
\begin{lstlisting}[language=Coq]
| E_MakeCSR_IDevID : forall stTPM st id crt k,
    In (signedCertificate crt) st ->
    In (publicKey k) st ->
    execute (stTPM, st)
            (MakeCSR_IDevID id crt k)
            (stTPM, TCG_CSR_IDevID id crt k :: st)

| E_MakeCSR_LDevID : forall stTPM st m crt,
    In m st ->
    In (signedCertificate crt) st ->
    execute (stTPM, st)
            (MakeCSR_LDevID m crt)
            (stTPM, TCG_CSR_LDevID m crt :: st)

| E_CheckCert : forall stTPM st k id i r s d f,
    In (signedCertificate (Cert k id (Private i r s d f))) st ->
    In (publicKey (Public i r s d f)) st ->
    execute (stTPM, st)
            (CheckCert (Cert k id (Private i r s d f)) (Public i r s d f))
            (stTPM, st)

| E_CheckAttributes : forall stTPM st i r s d f,
    In (publicKey (Public i r s d f)) st ->
    execute (stTPM, st)
            (CheckAttributes (Public i r s d f) r s d f)
            (stTPM, st)

| E_MakePair : forall stTPM st m1 m2,
    In m1 st ->
    In m2 st ->
    execute (stTPM, st)
            (MakePair m1 m2)
            (stTPM, pair m1 m2 :: st).
\end{lstlisting}
\end{figure}